import com.cloudbees.groovy.cps.NonCPS
import org.jenkinsci.plugins.workflow.steps.FlowInterruptedException

import static java.util.UUID.randomUUID

@Library("jenlib") _

/**
 * Root for all calibration deployments done by this Jenkinsfile.
 * Folder structure will follow this blueprint: '$deploymentRoot/$setupUID/{stable/testing}/{date,cXXXpXX_date}'
 */
final String deploymentRoot = "/wang/data/calibration/hicann-dls-sr-hx/"
final boolean run_weekly_calib = env.JOB_NAME.contains("weekly")

enum HxVersion {
	HXv3(3)

	final int chipRevision

	HxVersion(int chipRevision) {
		this.chipRevision = chipRevision
	}
}

class HxCubeSetup implements Serializable {
	static final int GERRIT_HXCUBE_ID = 2

	int hxCubeId
	int fpgaId
	int chipId
	String uniqueIdentifier

	HxCubeSetup(int hxCubeId, int fpgaId, int chipId, String uniqueIdentifier) {
		this.hxCubeId = hxCubeId
		this.fpgaId = fpgaId
		this.chipId = chipId
		this.uniqueIdentifier = uniqueIdentifier
	}

	int getWaferId() {
		return hxCubeId + 60
	}

	@NonCPS
	@Override
	String toString() {
		return "HxCubeSetup(${uniqueIdentifier})"
	}
}

/**
 * Compile a list of hx cube setups as listed in the hwdb. Filter on a given HICANN-X revision.
 */
List<HxCubeSetup> getCubeSetups(HxVersion version) {
	String hwdbQuery = """
from typing import Dict
from numbers import Integral
import json
from pyhwdb import HXCubeSetupEntry, database


def get_hxcube_entries(database: database) -> Dict[Integral, HXCubeSetupEntry]:
    return {i: database.get_hxcube_setup_entry(i) for i in database.get_hxcube_ids()}


def serialize_setups(setups: Dict[Integral, HXCubeSetupEntry]) -> str:
    list_setups = []
    for setup_id, setup in setups.items():
        for i, fpga in setup.fpgas.items():
            if fpga.wing:
                fields = {
                    "setup_id": str(setup_id),
                    "fpga": str(i),
                    "handwritten_chip_serial": str(fpga.wing.handwritten_chip_serial),
                    "chip_revision": str(fpga.wing.chip_revision),
                    "unique_id": setup.get_unique_branch_identifier(fpga.wing.handwritten_chip_serial),
                }
                list_setups.append(fields)
    return json.dumps(list_setups)


if __name__ == '__main__':
    db = database()
    db.load(db.get_default_path())
    hx_setups = get_hxcube_entries(db)
    print(serialize_setups(hx_setups))
"""
	String queryResultString = null
	runOnSlave(label: "frontend") {
		String tempFilePath = "${pwd(tmp: true)}/${randomUUID().toString()}.py"
		writeFile(file: tempFilePath, text: hwdbQuery)
		withModules(modules: ["localdir"]) {
			queryResultString = jesh(script: "python ${tempFilePath}", returnStdout: true).trim()
		}
	}
	List<Object> queryResult = readJSON(text: queryResultString, returnPojo: true)

	return queryResult.findAll { it.chip_revision.toInteger() == version.chipRevision }.collect {
		new HxCubeSetup(((String) it.setup_id).toInteger(),
		                ((String) it.fpga).toInteger(),
		                ((String) it.handwritten_chip_serial).toInteger(),
		                (String) it.unique_id)
	}
}

Closure calibrationPipeline = { HxVersion version ->
	if (version == HxVersion.HXv3)
	{
		wafSetup(projects: ["calix", "hwdb"])
	}

	stage("Build for ${version}") {
		onSlurmResource(partition: "jenkins", "cpus-per-task": "8") {
			withCcache() {
				jesh("waf configure install --build-profile=ci --test-execnone --with-pybind")
			}
		}
	}

	Set<HxCubeSetup> calibratedSetups = []
	stage("Setup IDs for ${version}") {
		List<HxCubeSetup> setups = getCubeSetups(version)

		// add random Jenkins setup
		List<HxCubeSetup> jenkins_setups = setups.findAll { it.hxCubeId == HxCubeSetup.GERRIT_HXCUBE_ID }
		Collections.shuffle(jenkins_setups)
		calibratedSetups.add(jenkins_setups.first())

		if (!isTriggeredByGerrit()) {
			calibratedSetups.addAll(setups)
		}

		echo("Calibrating ${version} setups: ${calibratedSetups}")
	}

	Map<String, Closure> calibrationRuns = calibratedSetups.collectEntries { setup ->
		[((String) "Setup: ${setup.uniqueIdentifier}"): {
			String slurmTimeout = (run_weekly_calib) ? "4:00:00" : "50:00"

			conditionalTimeout(enable: !isTriggeredByGerrit(), time: 4, unit: "HOURS") {
				onSlurmResource(partition: "cube",
				                wafer: setup.waferId,
				                "fpga-without-aout": setup.fpgaId,
				                time: slurmTimeout) {
					if (setup.hxCubeId == setup.GERRIT_HXCUBE_ID) {
						configureHxCubeBitfile()
					}

					String deploymentFolder = createDeploymentDirectory("${deploymentRoot}/${setup.uniqueIdentifier}")
					boolean calibrationSuccessful = jesh(script: "calix_generate_default_calibration.py ${deploymentFolder}",
					                                     returnStatus: true) == 0

					boolean weeklyCalibSuccessful = false
					if (run_weekly_calib)
					{
						weeklyCalibSuccessful = jesh(script: "calix_generate_weekly_calibration.py ${deploymentFolder}",
					                                     returnStatus: true) == 0
					}

					if (jesh(script: "basename \"\$(dirname \\\"${deploymentFolder}\\\")\"",
					         returnStdout: true).trim() == "stable") {
						if (calibrationSuccessful)
						{
							jesh("ln -sfT \"${deploymentFolder}\" \$(dirname \"${deploymentFolder}\")/latest")
						}
						if (weeklyCalibSuccessful) {
							jesh("ln -sfT \"${deploymentFolder}\" \$(dirname \"${deploymentFolder}\")/latest-weekly")
						}
					}

					if (!calibrationSuccessful) {
						unstable("Calibration was not successful.")
					}
					if (run_weekly_calib && (!weeklyCalibSuccessful)) {
						unstable("Weekly calibration was not successful.")
					}
				}
			}
		}]
	}

	stage("Run Calibrations for ${version}") {
		withModules(modules: ["localdir"]) {
			try {
				parallel calibrationRuns
			} catch (FlowInterruptedException ignored) {
				unstable("Calibration not successful: Execution has been interrupted.")
			}
		}
	}
}


try {
	inSingularity(app: "dls") {
		withWaf() {
			HxVersion.values().each { version ->
				calibrationPipeline(version)

				runOnSlave(label: "frontend") {
					cleanWs()
				}
			}
		}
	}
} catch (Throwable t) {
	notifyFailure(mattermostChannel: "#dls-software")
	throw t
}
